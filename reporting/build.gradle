buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath group: 'gradle.plugin.com.webcohesion.enunciate', name: 'enunciate-gradle', version: '2.0.0'
    classpath 'net.saliman:gradle-cobertura-plugin:2.3.0'
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'net.saliman.cobertura'

cobertura {
  coverageFormats = ['html', 'xml']
}

repositories {
    jcenter()
    maven {
        url 'https://artifacts.dev.ghap.io/artifactory/ext-snapshot-local'
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

    resolutionStrategy {
        // Cobertura includes an ASM version that can't handle Java 8, ASM 5.0.3 handles Java8
        force 'org.ow2.asm:asm:5.0.4'
        forcedModules = [ 'org.ow2.asm:asm:5.0.4' ]
    }
}

dependencies {
    compile 'com.netflix.governator:governator-servlet:1.3.3'
    compile 'com.sun.jersey:jersey-json:1.19'
    compile 'com.sun.jersey.contribs:jersey-guice:1.19'
    compile 'com.google.inject.extensions:guice-servlet:4.0'
    compile 'com.google.inject.extensions:guice-persist:4.0'
    compile 'com.google.inject.extensions:guice-multibindings:4.0'
    compile 'com.google.inject.extensions:guice-throwingproviders:4.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.amazonaws:aws-java-sdk:1.9.30'
    compile 'org.hibernate:hibernate-core:4.3.8.Final'
    compile 'org.hibernate:hibernate-entitymanager:4.3.8.Final'
    compile 'org.hibernate:hibernate-c3p0:4.3.8.Final'
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'org.joda:joda-convert:1.8.1'
    compile ('io.ghap:ghap-security-client:1.0-SNAPSHOT') {
        changing = true
    }
    compile ('io.ghap:aws-helper-utilities:1.0-SNAPSHOT') {
        changing = true
    }
    compile ('io.ghap:log-event-client:1.0-SNAPSHOT') {
        changing = true
    }
    compile 'com.sun.grizzly:grizzly-webserver:1.9.62'
    compile 'org.apache.commons:commons-csv:1.2'
    compile 'commons-beanutils:commons-beanutils:1.9.2'
    compile 'org.slf4j:jul-to-slf4j:1.7.10'
    providedCompile 'javax.servlet:servlet-api:2.5'
    testCompile 'junit:junit:4.10'
    testCompile 'com.netflix.governator:governator-test-junit:1.3.3'
    testCompile 'com.github.bijukunjummen:governator-junit-runner:0.2.RELEASE'
    testCompile 'com.netflix.governator:governator-jetty:1.9.3'

    runtime 'ch.qos.logback:logback-classic:1.1.3'

    // configuration
    compile 'com.netflix.archaius:archaius-aws:0.6.6'
}

apply plugin: 'com.webcohesion.enunciate'

tasks.enunciate {
  configFileName = "enunciate.xml"
  File enunciateDistDir = file("src/main/webapp")
  export("docs", enunciateDistDir)
}

task explodedWar(type: Copy) {
  into "$buildDir/exploded"
  with war
}

war {
  from('resources') {
    include '*.properties'
    into 'WEB-INF/classes'
  }
  webXml = file('src/main/webapp/WEB-INF/web.xml')
}

war.dependsOn(['explodedWar', 'enunciate'])

jettyRunWar.contextPath = ''
jettyRun.contextPath = ''

test {
    systemProperties = [
            "archaius.deployment.environment": System.getProperty("archaius.deployment.environment", "samba"),
            "s3.config.bucket": System.getProperty("s3.config.bucket", "ghap-configuration-us-east-1-samba")
    ]
}
