buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath group: 'gradle.plugin.com.webcohesion.enunciate', name: 'enunciate-gradle', version: '2.0.0'
    classpath "net.saliman:gradle-cobertura-plugin:2.2.8"
  }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'net.saliman.cobertura'

cobertura {
  coverageFormats = ['html', 'xml']
}

repositories {
    jcenter()
    maven {
        url 'https://artifacts.dev.ghap.io/artifactory/ext-snapshot-local'
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile 'com.netflix.governator:governator-servlet:1.3.3'
    compile 'com.sun.jersey:jersey-json:1.19'
    compile 'com.sun.jersey.contribs:jersey-multipart:1.19'
    compile 'com.sun.jersey.contribs:jersey-guice:1.19'
    compile 'com.amazonaws:aws-java-sdk:1.9.30'
    compile 'com.google.code.gson:gson:2.3.1'
    compile ('io.ghap:ghap-security-client:1.0-SNAPSHOT') {
        changing = true
    }
    compile ('io.ghap:log-event-client:1.0-SNAPSHOT') {
        changing = true
    }
    compile 'org.slf4j:slf4j-api:1.7.10'
    compile 'org.codehaus.plexus:plexus-archiver:2.2'
    runtime 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'junit:junit:4.10'
    testCompile 'com.netflix.governator:governator-test-junit:1.3.3'
    testCompile 'com.github.bijukunjummen:governator-junit-runner:0.2.RELEASE'

    // configuration
    compile 'com.netflix.archaius:archaius-aws:0.6.6'

    // http://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

}

apply plugin: 'com.webcohesion.enunciate'

tasks.enunciate {
  configFileName = "enunciate.xml"
  File enunciateDistDir = file("src/main/webapp")
  include("**/io/ghap/**/*.java")
  exclude("**/io/ghap/**/*Test*.java")
  export("docs", enunciateDistDir)
}

task explodedWar(type: Copy) {
  into "$buildDir/exploded"
  with war
}

war {
  from('resources') {
    include '*.properties'
    into 'WEB-INF/classes'
  }
  webXml = file('src/main/webapp/WEB-INF/web.xml')
}

war.dependsOn(['explodedWar', 'enunciate'])

jettyRunWar.contextPath = ''
jettyRun.contextPath = ''
